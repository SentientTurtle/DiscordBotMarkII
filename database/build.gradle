plugins {
    id 'java'
}

java {
    sourceCompatibility = JavaVersion.VERSION_16
    targetCompatibility = JavaVersion.VERSION_16
}

group 'net.sentientturtle'
version '0.1'

compileJava {
    options.encoding = "UTF-8"
    options.compilerArgs << '-Xlint:unchecked'
    options.compilerArgs << '-Xlint:deprecation'
}

compileTestJava {
    options.encoding = "UTF-8"
    options.compilerArgs << '--enable-preview'

    options.compilerArgs << '-Xlint:unchecked'
    options.compilerArgs << '-Xlint:deprecation'
}

repositories {
    mavenCentral()
    maven {
        name 'm2-dv8tion'
        url 'https://m2.dv8tion.net/releases'
    }
}

configurations {
    native_libs
}

task installNativeLibs(type: Copy) {
    from configurations.native_libs into "$sourceSets.main.output.resourcesDir/nativelibs"
}
compileJava.dependsOn installNativeLibs

dependencies {
    implementation project(':core')
    implementation 'com.almworks.sqlite4java:sqlite4java:1.0.392'
    native_libs group: 'com.almworks.sqlite4java', name: 'sqlite4java-win32-x64', version: '1.0.392'
    native_libs group: 'com.almworks.sqlite4java', name: 'sqlite4java-win32-x86', version: '1.0.392'
    native_libs group: 'com.almworks.sqlite4java', name: 'libsqlite4java-linux-amd64', version: '1.0.392'
    native_libs group: 'com.almworks.sqlite4java', name: 'libsqlite4java-linux-i386', version: '1.0.392'
    native_libs group: 'com.almworks.sqlite4java', name: 'libsqlite4java-osx', version: '1.0.392'
}
